|**
	Buff and Raid buffbot v2.0 Original Author: Blasty
	Maintained for RedGuides by Blasty 2021-03-29
	Purpose: A bot that can do buffs and raid buffs.
	Feature: Whitelisting, Blacklisting, Guildies only, Only private tells, Bot managers, Spellsets, Botlogs and Spam protection
	Recommended: Use a private location for this macro such as a private guild lobby
**| 

#Event Hail "#1# says,#*#'Hail, |${Me.Name}|'#*#"
#Chat	Tell

sub main
	/declare	debug				bool	outer	FALSE
	/declare	runCheck			bool	outer	FALSE
	/declare	validSender			bool	outer	TRUE
	/declare 	IniFileName			string	outer	BarBot2.ini
	/declare 	CurrentVersion		int		outer	2.0
	/declare	i					int		outer	0
	/declare 	medUntil 			int 	outer 	98
	/declare 	CastSpellInitiated 	bool 	outer 	FALSE
	/declare 	AntiSpam[1,3]		string	outer	0
	/declare 	ZoneID 				int 	local	${Zone.ID}
	
	/call LoadIniFile
	/call Startup
			
	:loop
		/varset validSender TRUE
		/delay 1s
		/if ( ${CampIfOutOfFoodAndDrink} && (${FindItemCount[${Food}]} < 1 || ${FindItemCount[${Drink}]} < 1) ) {
			/camp
			/call Log "Out of food and/or water - Camping!"
			/end
		}
		/if (${ZoneID} != ${Zone.ID}) {
			/camp
			/delay 50
			/exit
		}
		/if ( ${Window[TradeWnd].Open} ) /call TradeActive
		/if (${Cursor.ID}) /destroy
		/doevents
	/goto :loop
/return

Sub Chat(string ChatSender, string Message)
	/target id ${Spawn[PC ${ChatSender}].ID}
	/if ( ${Target.ID} ==  ${Me.ID}) /return
	/tell ${ChatSender} ${Message}
	/delay 20
/return

Sub Log(string LogEntry)
	/if ( ${UseLogToFile} ) /mqlog BufferBot ${Me.CleanName}: ${LogEntry}
/return

Sub Startup 
	/if ( !${Me.Sitting} ) /sit
	/if ( ${SpellSet.NotEqual[null]} ) /memspellset ${SpellSet}
	/if (${Me.AFK}) /afk off
	/echo Starting BaRBot by Blasty
	/if ( ${UseWhitelist} ) /echo Whitelisting enabled
	/if ( ${BuffGuildOnly} ) /echo BuffGuildOnly enabled
/return

Sub TradeActive
	/call ValidateSender ${Target.CleanName}
	/if ( !${validSender} ) /return
	/if (!${Me.FreeInventory}) {
		/notify TradeWnd TRDW_Cancel_Button leftmouseup
		/call Log "Inventory is full!"
	}
	/delay 10
	/if (${Window[TradeWnd].HisTradeReady}) {
		/for i 8 to 15
			/if ( ${Window[TradeWnd].Child[TRDW_TradeSlot${i}].Tooltip.Length} > 0 && !${AcceptedItems.Find[${Window[TradeWnd].Child[TRDW_TradeSlot${i}].Tooltip}]} ) {
				/echo ${Target.CleanName} "Sorry, I do not accept ${Window[TradeWnd].Child[TRDW_TradeSlot${i}].Tooltip}"
				/notify TradeWnd TRDW_Cancel_Button leftmouseup
				/return
			}
		/next i
		/notify TradeWnd TRDW_Trade_Button leftmouseup
		/call Chat ${Target.CleanName} "Thanks!"
	}
/return

Sub CastSpell(string ChatSender, string Command)
	/target id ${Spawn[PC ${ChatSender}].ID}
	/if ( ${Target.ID} ==  ${Me.ID}) /return
		
	/if ( ${AntiSpam[1,1].Equal[${ChatSender}]} && ${AntiSpam[1,2].Equal[${Command}]} && ${AntiSpam[1,3].NotEqual[null]} ) {
		/call Log "SPAM ${ChatSender} ${Command}"
		/return
	} 
	/if ( ${AntiSpam[1,1].Equal[${ChatSender}]} && ${AntiSpam[1,2].Equal[${Command}]}) {
		/if ( !${AntiSpam[1,3]} ) {
			/varset AntiSpam[1,3] Blocked
			/call Chat ${ChatSender} "Attention: Do not spam"
		}
	} else {
		/varset AntiSpam[1,1] ${ChatSender}
		/varset AntiSpam[1,2] ${Command}
		/varset AntiSpam[1,3] null
	}
	
	/if ( ${Me.PctMana} < ${medUntil} ) {
		/if ( !${Me.Sitting} ) /sit
		/call Chat ${ChatSender} "Please wait while I med to full (Current at ${Me.PctMana}% mana)..."
		/call Chat ${ChatSender} "Buffing will continue in a few!"
		:med
		/if (${Me.PctMana} < ${medUntil}) {
			/delay 100
			/call Chat ${ChatSender} "${Me.PctMana}%"
			/goto :med
		}
	}
	/call Chat ${ChatSender} "Please be patient while buffing!"
	/for i 1 to ${NumberOfSpells}
		/if ( !${SpellSkip${i}} && (${Command.Equal[${SpellActivationWord${i}}]} || ${Select[${Command},${SpellActivationWords${i}}]} || ${Command.Equal[raid]} ) ) {
			/if ( ${SpellRaidBuffs${i}} == FALSE && ${Select[${Command},raid]}==1 ) {
				/echo "Raid buffs disabled"
				/goto :end
			}
			/if ( ${SpellRaidBuffs${i}} == TRUE && ${Select[${Command},raid]}==1 ) {
				:wait
				/if (!${Me.AltAbilityReady[Tranquil Blessings]}) {
					/if ( !${Me.Sitting} ) /sit
					/delay 20
					/goto :wait
				}			
				/alt act 992
				/delay 10
			}
			/casting "${SpellID${i}}"
			/while (${Cast.Status.Find[C]}) {
				/delay 10
			}
			/if ( ${Cast.Result.Equal[CAST_SUCCESS]} ) {
				/call Log "CAST_SUCCESS (${SpellName${i}}) on ${ChatSender}"
			} else /if (${Cast.Result.Equal[CAST_TAKEHOLD]}) {
				/call Chat ${ChatSender} "Error - Buff (${SpellName${i}}) did not take hold!"
				/call Log "CAST_TAKEHOLD (${SpellName${i}}) on ${ChatSender}"
			} else /if (${Cast.Result.Equal[CAST_COMPONENTS]}) {
				/call chat ${ChatSender} "Error - Missing reagents! Please go buy some ${Reagent${i}} and trade it to me."
				/call Log "CAST_COMPONENTS (${SpellName${i}}) has no reagents. Requested by: ${ChatSender}"
			} else {
				/call Log "Error! CAST_ERROR: ${Cast.Result}"
			}
			/varset CastSpellInitiated TRUE
		}
	:end
	/next i
	/if ( ${CastSpellInitiated} ) {
			/call Chat ${ChatSender} "Finished"
			/if ( !${Me.Sitting} ) /sit
			/varset CastSpellInitiated FALSE
	} else {
		/call Chat ${ChatSender} "Did you know that Blasty.... Ehm what?"
		/call Log "Error! Unkown command from ${ChatSender}: ${Command}"
	}
/return

Sub ShowList(string ChatSender)
		/call Chat ${ChatSender} "---> COMMANDS <---"
		/delay 20
		/for i 1 to ${NumberOfSpells}
			/if ( ${debug} ) /echo  for i NumberOfSpells: ${SpellActivationWord${i}} = ${SpellName${i}}
			/if ( !${SpellSkip${i}} ) {
				/call Chat ${ChatSender} "${SpellActivationWord${i}} = ${SpellName${i}}"
				/delay 20
			}
		/next i
		/if ( ${Select[${ChatSender},${BotManagers}]} ) {
			/call Chat ${ChatSender} "--> BOTMANAGERS <--"
			/delay 20
			/call Chat ${ChatSender} "Reload = Reload the macro"
			/delay 20
			/call Chat ${ChatSender} "Camp = Camp the bot"
			/delay 20
			/call Chat ${ChatSender} "Exit = Exit Everquest"
			/delay 20
			/call Chat ${ChatSender} "Stand = Make the toon /stand"
			/delay 20
			/call Chat ${ChatSender} "Sit = Make the toon /sit"
			/delay 20
			/call Chat ${ChatSender} "Follow = Make the toon /follow"
			/delay 20
			/call Chat ${ChatSender} "Status = Shows Iron Ration / Water Flask amount"
			/delay 20
			/call Chat ${ChatSender} "PauseBot = Pause the bot equal not buffing"
			/delay 20
			/call Chat ${ChatSender} "unPauseBot = Unpause the bot equal continue buffing"
			/delay 20
		}
		/call Chat ${ChatSender} "--- END OF COMMANDS ---"
/return

Sub ValidateSender(string ChatSender)
	/if ( ${BuffGuildOnly} && ${Spawn[PC ${ChatSender}].Guild.NotEqual[${Me.Guild}]} ) {
		/call Log "Not guilde hail: ${ChatSender}"
		/varset validSender FALSE
		/if ( ${debug} ) /echo BuffGuildOnly TRUE
	}
	/if ( ${Select[${ChatSender},${BlacklistedPlayers}]} ) {
		/call Log "Blacklisted hail: ${ChatSender}"
		/varset validSender FALSE
		/if ( ${debug} ) /echo BlacklistedPlayers TRUE
	}
	/if ( ${UseWhitelist} && !${Select[${ChatSender},${WhitelistedPlayers}]} ) {
		/call Log "${ChatSender} not whitelisted"
		/varset validSender FALSE
		/if ( ${debug} ) /echo UseWhitelist TRUE
	}
	/if ( !${Spawn[PC ${ChatSender}].LineOfSight} ) {
		/if ( ${debug} ) /echo LineOfSight TRUE
		/varset validSender FALSE
		/return
	}
	/if (${FindItemCount[=Iron Ration]} < 10 && !${Select[${ChatSender},${BotManagers}]}) {
		/call Chat ${ChatSender} "I'm running low on 'Iron Ration' Please go buy some and trade it to me and then I can help you!"
		/varset validSender FALSE
	}
	/if (${FindItemCount[=Water Flask]} < 10 && !${Select[${ChatSender},${BotManagers}]}) {
		/call Chat ${ChatSender} "I'm running low on 'Water Flask' Please go buy some and trade it to me and then I can help you!"
		/varset validSender FALSE
	}
/return

Sub event_Hail(string RawChat, string PlayerName)
	/call ValidateSender ${PlayerName}
	/if ( !${validSender} ) /return
	/if ( ${runCheck}== TRUE ) /return
	/varset runCheck TRUE
	/call Chat ${PlayerName} "${HailResponse}"
	/call Log "Hail from ${PlayerName}"
	/doevents flush
	/varset runCheck FALSE
/return

Sub Event_Chat(string ChatType, string ChatSender, string ChatText)
	/call ValidateSender ${ChatSender}
	/if ( ${debug} ) /echo Event_Chat ValidateSender TRUE
	/if ( !${validSender} ) /return
	/if ( ${debug} ) /echo validSender TRUE
	/if ( ${Select[${ChatSender},${BotManagers}]} ) {
			/if ( ${ChatText.Find[Exit]} ) {
				/call Log "${ChatSender} executed Exit command"
				/exit
				/return
			}
			/if ( ${ChatText.Find[Camp]} ) {
				/call Log "${ChatSender} executed Camp command"
				/camp
				/return
			}
			/if ( ${ChatText.Find[Reload]} ) {
				/call Log "${ChatSender} executed Reload command"
				/doevents flush
				/if ( !${Me.Sitting} ) /sit
				/return
			}
			/if ( ${ChatText.Find[Stand]} ) {
				/call Log "${ChatSender} executed Stand command"
				/stand
				/return
			}
			/if ( ${ChatText.Find[Sit]} ) {
				/call Log "${ChatSender} executed Sit command"
				/sit
				/return
			}
			/if ( ${ChatText.Find[Follow]} ) {
				/call Log "${ChatSender} executed Follow command"
				/if ( !${Me.Standing} ) /stand
				/tar ${ChatSender}
				/follow
				/call Chat ${ChatSender} "Following. Make sure to 'follow' when done and also a 'Reload'"
				/return
			}
			/if ( ${ChatText.Find[PauseBot]} ) {
				/call Log "${ChatSender} executed PauseBot command"
				/varset runCheck TRUE
				/return
			}
			/if ( ${ChatText.Find[unPauseBot]} ) {
				/call Log "${ChatSender} executed unPauseBot command"
				/varset runCheck FALSE
				/return
			}
			/if ( ${ChatText.Find[status]} ) {
				/call Chat ${ChatSender} "Iron Ration:${FindItemCount[=Iron Ration]} | Water Flask:${FindItemCount[=Water Flask]}"				 
				/return
			}
			/if ( ${ChatText.Find[A.F.K]} ) {
				/return
			}
			/if ( ${ChatText.Find[test]} ) {
				/return
			}
	}
	/if ( ${runCheck}== TRUE ) /return
	/varset runCheck TRUE
	/if ( ${debug} ) /echo Chatlength: ${ChatText.Length}
	/if ( ${ChatText.Find[list]} && ${ChatText.Length} == 5) {	
		/call Log "List of commands requested from ${ChatSender}"
		/call ShowList ${ChatSender}
		/varset runCheck FALSE
		/if ( ${debug} ) /echo list commands TRUE
		/doevents flush
		/return
	}
	/call CastSpell ${ChatSender} ${ChatText.Right[-1]}
	/varset runCheck FALSE
/return

Sub LoadIniFile
	/call InitiateIniFile string 	General 	SpellSet
	/call InitiateIniFile bool 		General 	BuffGuildOnly				FALSE		
	/call InitiateIniFile bool 		General 	UseLogToFile				FALSE
	/call InitiateIniFile string 	General 	BlacklistedPlayers
	/call InitiateIniFile string 	General 	WhitelistedPlayers
	/call InitiateIniFile string 	General 	BotManagers
	/call InitiateIniFile bool 		General 	CampIfOutOfFoodAndDrink 	TRUE
	/call InitiateIniFile string 	General 	Food						"Iron Ration"
	/call InitiateIniFile string	General 	Drink						"Water Flask"
	
	/call InitiateIniFile string	${Me.Class}	HailResponse				"Send me a tell with 'Buffs' to get all buffs. To se my individual commands send me a tell with 'List'"
	/call InitiateIniFile string	${Me.Class}	AcceptedItems
	/call InitiateIniFile int		${Me.Class}	NumberOfSpells				1
	
	/for i 1 to ${NumberOfSpells}
		/call InitiateIniFile	string		${Me.Class}	SpellName${i}
		/call InitiateIniFile 	int			${Me.Class}	SpellID${i}
		|/call InitiateIniFile	string		${Me.Class}	SpellGemLocation${i}
		/call InitiateIniFile 	string		${Me.Class}	SpellActivationWords${i}	"Buff,Buffs"
		/call InitiateIniFile 	string		${Me.Class}	SpellActivationWord${i}	
		/call InitiateIniFile	bool		${Me.Class}	SpellRaidBuffs${i}			FALSE
		/call InitiateIniFile	bool		${Me.Class}	SpellSkip${i}				FALSE
		/call InitiateIniFile	string		${Me.Class}	Reagent${i}
	/next i
/return

Sub InitiateIniFile(string varType, string category, string VarName, string varValue)
	/declare ${VarName} ${varType} outer
	/declare TmpVar string local ${Ini[${IniFileName},${category},${VarName}]}
	 /if ( ${TmpVar.Equal[null]} ) { 
		/varset ${VarName} ${varValue}
		/ini "${IniFileName}" "${category}" "${VarName}" "${varValue}"
	} else {
		/varset ${VarName} ${TmpVar}	
	}
/return